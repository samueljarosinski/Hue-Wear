apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: rootProject.file('gradle/checks/detekt.gradle')
apply from: rootProject.file('gradle/checks/checkstyle.gradle')
apply from: rootProject.file('gradle/checks/spotbugs.gradle')

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId 'io.github.samueljarosinski.huewear'

        versionName '0.1.0'
        versionCode 1

        minSdkVersion 25
        targetSdkVersion 29

        vectorDrawables.useSupportLibrary true
        resConfigs 'en'
    }

    signingConfigs {
        release {
            def signingFile = rootProject.file('signing.properties')

            if (signingFile.canRead()) {
                Properties signingProperties = new Properties()
                signingProperties.load(new FileInputStream(signingFile))

                storeFile rootProject.file(signingProperties['storeFile'])
                storePassword signingProperties['storePassword']
                keyAlias signingProperties['keyAlias']
                keyPassword signingProperties['keyPassword']
            }
        }

        debug {
            storeFile rootProject.file('debug.keystore')
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            crunchPngs true

            signingConfig signingConfigs.release

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'

            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        incremental true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        dexInProcess true
        preDexLibraries true
        maxProcessCount 4
        javaMaxHeapSize '4g'
    }

    kapt {
        useBuildCache true
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError true
        warningsAsErrors true
        ignoreWarnings false
        xmlReport false
        lintConfig file("${project.rootDir}/config/lint/lint.xml")
        htmlOutput file("${project.buildDir}/reports/lint/lint.html")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "androidx.core:core-ktx:1.0.2"
    implementation "androidx.wear:wear:1.0.0"

    implementation "com.google.android.support:wearable:2.4.0"
    compileOnly "com.google.android.wearable:wearable:2.4.0"

    implementation "com.jakewharton.timber:timber:4.7.1"
}
